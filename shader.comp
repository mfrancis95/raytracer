layout (local_size_x = 1, local_size_y = 1) in;

layout(binding = 3, std430) buffer PixelStorage {
    vec3 colours[];
};

void main() {
    vec3 colour = vec3(0, 0, 0);
    Intersection closestIntersection = {3.402823466e+38, {0, 0, 0}, {0, 0, 0}};
    Intersection intersection = closestIntersection;
    int primitive = -1;
    float aspectRatio = 1280.0 / 960;
    Ray ray = castRay(
        (float(gl_GlobalInvocationID.x) / 1280 - 0.5) * aspectRatio, float(gl_GlobalInvocationID.y) / 960 - 0.5
    );
    for (int i = 0; i < numPrimitives; i++) {
        if (intersect(primitives[i], ray, intersection) &&
            intersection.distance < closestIntersection.distance) {
            closestIntersection = intersection;
            primitive = i;
        }
    }
    if (primitive == -1) {
        colours[int(gl_GlobalInvocationID.x) + int(gl_GlobalInvocationID.y) * 1280] = colour;
        return;
    }
    colour = ambient;
    for (int i = 0; i < numLights; i++) {
        Ray lightRay = {
            normalize(lights[i].position - closestIntersection.point),
            closestIntersection.point
        };
        bool intersectsPrimitive = false;
        for (int j = 0; j < numPrimitives; j++) {
            if (j != primitive &&
                intersect(primitives[j], lightRay, intersection)) {
                intersectsPrimitive = true;
                break;
            }
        }
        if (!intersectsPrimitive) {
            colour += phongIlluminate(
                closestIntersection, lights[i], materials[primitive], ray
            );
        }
    }
    colours[int(gl_GlobalInvocationID.x) + int(gl_GlobalInvocationID.y) * 1280] = colour;
}